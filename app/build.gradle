apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.example.hongen.gradlelab"
        minSdkVersion 8
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        // dex 突破65535的限制
        multiDexEnabled true

        // 默认渠道号
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }
    
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // 多渠道打包Apk文件命名方法
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith(".apk")) {
                        def fileName = "hongen_${variant.productFlavors[0].name}_${defaultConfig.versionName}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    // 多渠道打包 -- 方式1
    /*productFlavors {
        xiaomi {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE:"xiaomi"]
        }

        baidu {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE:"baidu"]
        }
    } */

    // 多渠道打包 -- 方式2
    productFlavors {
        xiaomi {}
        baidu {}
    }
    
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
}
